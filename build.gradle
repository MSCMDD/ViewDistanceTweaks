plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'java'
}

group 'com.froobworld'
version '1.5.7'
jar.enabled = false

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots"
    }
    maven {
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    compileOnly 'io.papermc.paper:paper-api:1.20.4-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'io.netty:netty-all:4.1.74.Final'
    implementation 'org.jooq:joor:0.9.14'
    implementation 'com.froobworld:nab-configuration:1.0.2'
    implementation 'org.bstats:bstats-bukkit:3.0.0'
}

processResources {
    filter { String line -> line.replace('${version}', project.version.toString()) }
}

shadowJar {
    archiveFileName = 'ViewDistanceTweaks-' + project.version.toString() + '.jar'

    relocate 'com.froobworld.nabconfiguration', 'com.froobworld.viewdistancetweaks.lib.nabconfiguration'
    relocate 'org.joor', 'com.froobworld.viewdistancetweaks.lib.joor'
    relocate 'org.bstats', 'com.froobworld.viewdistancetweaks.lib.bstats'
}

artifacts {
    shadowJar
}

assemble.dependsOn(shadowJar)
